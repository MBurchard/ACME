plugins {
	id 'com.github.ben-manes.versions' version '0.21.0'
	id 'org.owasp.dependencycheck' version '4.0.2'
	id 'org.springframework.boot' version '2.1.5.RELEASE'
}

dependencyUpdates.resolutionStrategy {
	componentSelection { rules ->
		rules.all { ComponentSelection selection ->
			boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
				selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
			}
			if (rejected) {
				selection.reject('Release candidate')
			}
		}
	}
}

allprojects {
	group = 'de.mbur'
	version = '0.0.1-SNAPSHOT'

	task cleanOutput(type: Delete) {
		delete 'bin', 'build', 'out', 'target'
	}
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'org.owasp.dependencycheck'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	sourceCompatibility = '1.8'

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation 'org.slf4j:slf4j-api',
				'javax.inject:javax.inject:1'

		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'junit', module: 'junit' //by both name and group
		}
		testImplementation 'org.springframework.security:spring-security-test',
				'org.junit.jupiter:junit-jupiter-api',
				'org.junit.jupiter:junit-jupiter-params'

		testRuntime 'org.junit.jupiter:junit-jupiter-engine'
	}

	compileJava.dependsOn(processResources)
	compileJava.options.encoding = 'UTF-8'
}
